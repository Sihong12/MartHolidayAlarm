buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
        querydslVersion = '4.2.1'
        swaggerVersion = '2.8.0'
        asciidoctorVersion = '1.5.3'
        restDocsVersion = '2.0.2.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

group = 'com.hongsi'
version = '0.0.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-aop')

    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.2.3'
    compile group: 'com.querydsl', name: 'querydsl-jpa', version: "${querydslVersion}"
    compile group: 'com.querydsl', name: 'querydsl-apt', version: "${querydslVersion}"

    compile group: 'io.springfox', name: 'springfox-swagger2', version: "${swaggerVersion}"
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: "${swaggerVersion}"
    compile group: 'io.springfox', name: 'springfox-core', version: "${swaggerVersion}"

    compile group: 'com.google.firebase', name: 'firebase-admin', version: '6.4.0'

    runtime('com.h2database:h2')
    compileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}")
    asciidoctor("org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}")
}

// QueryDSL Configuration
def querydslOutput = file("src/main/generated")
sourceSets {
    main {
        java {
            srcDir querydslOutput
        }
    }
}

task generateQuerydsl(type: JavaCompile, group: 'build') {
    querydslOutput.deleteDir()
    querydslOutput.mkdirs()

    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = querydslOutput
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    doLast {
        println ">> Done generateQuerydsl"
    }
}

compileJava.dependsOn(generateQuerydsl)

clean.doLast {
    querydslOutput.deleteDir()
    println ">> Delete $querydslOutput"
}

// Rest Dos Configuration
ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs/'
    }
}